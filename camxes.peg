# GRAMMAR
text <- predicate space? EOF?

predicate <- predicate_place* p_predicate_tail_open_elidible predicate_tail 
predicate_tail <- predicate_tail_1 (predicate_connective p_predicate_tail_open_elidible predicate_tail_1 predicate_tail_terms )*
predicate_tail_1 <- p_neg_pre* (relation predicate_tail_terms / predicate_tail_pre)
predicate_tail_pre <- (pre_predicate_connective p_predicate_tail_open_elidible predicate_tail (pre_connective_separator p_predicate_tail_open_elidible predicate_tail)+ p_pre_connective_close_elidible) predicate_tail_terms

predicate_tail_terms <- predicate_place* p_predicate_tail_close_elidible
predicate_place <- p_predicate_place predicate_term

predicate_term <- predicate_term_1 (predicate_connective !p_predicate_tail_open predicate_term_1)*
predicate_term_1 <- predicate_term_pre / relation
predicate_term_pre <- (pre_predicate_connective !p_predicate_tail_open predicate_term (pre_connective_separator !p_predicate_tail_open predicate_term)+ p_pre_connective_close_elidible)

relation <- relation_1
relation_1 <- relation_2+ 
relation_2 <- relation_3 (relation_connective relation_3)*
relation_3 <- relation_pre / lex_relation / foreign_relation / foreign_quote / grammatical_quote / one_word_quote / ungrammatical_quote / abstraction / relation_place_swap / scoped_compound / space? relation_word
relation_pre <- (pre_relation_connective relation (pre_connective_separator relation)+ p_pre_connective_close_elidible)

lex_relation <- lex_relation_1 / lex_relation_2 / lex_relation_3 / lex_relation_4  / lex_relation_n

lex_relation_1 <- p_lex_relation_1 lex_relation_word
lex_relation_2 <- p_lex_relation_2 lex_relation_word lex_relation_word
lex_relation_3 <- p_lex_relation_3 lex_relation_word lex_relation_word lex_relation_word
lex_relation_4 <- p_lex_relation_4 lex_relation_word lex_relation_word lex_relation_word lex_relation_word
lex_relation_n <- p_lex_relation_n (!p_lex_relation_n word)+ p_lex_relation_n
lex_relation_word <- initial_dot word

foreign_relation <- p_foreign_relation_n_open foreign_relation_content dot? (space_char / EOF)
foreign_relation_content <- space_char initial_dot foreign_word (!space word)*

grammatical_quote <- p_grammatical_quote_open text p_grammatical_quote_close
one_word_quote <- p_one_word_quote space? word
ungrammatical_quote <- p_ungrammatical_quote_open (!p_ungrammatical_quote_close space? word) p_ungrammatical_quote_close

foreign_quote <- p_foreign_quote (space?) foreign_quote_open space (foreign_quote_word space)* foreign_quote_close

abstraction <- p_abstraction_open predicate p_abstraction_close_elidible
relation_place_swap <- p_place_swap relation_3

scoped_compound <- p_scope_open relation_1 p_scope_close_elidible

predicate_connective <- p_neg_pre? p_place_swap? p_predicate_connective p_neg_post?
relation_connective <- p_neg_pre? p_place_swap? p_relation_connective p_neg_post?

pre_predicate_connective <- p_pre_connective_open p_place_swap? p_predicate_connective p_neg_post?
pre_relation_connective <- p_pre_connective_open p_place_swap? p_relation_connective p_neg_post?
pre_connective_separator <- p_pre_connective_separator p_neg_post?

# PARTICLES (tmp)
# - Lexicalized relation
#   A string of words (particle or relation) can be lexicalized to have a unique relation meaning.
#
#   The particle depends on how many words form the lexicalized relation. 
#   More than 4 words requires a closing particle.
#   Note : Space or dot IS REQUIERED
p_lex_relation_1 <- space? (a)
p_lex_relation_2 <- space? (e)
p_lex_relation_3 <- space? (i)
p_lex_relation_4 <- space? (o)
p_lex_relation_n <- space? (u)

# - Abstractions
#   x1 is abstraction of [predicate] ...
p_abstraction_open <- !p_abstraction_close space? (b (vowel / diphtong)) 
p_abstraction_close_elidible <- p_abstraction_close?
p_abstraction_close <- space? (b a i) 

# - Foreign relation
#   A string of words used without their meaning to express some foreign concept.
#   x1 is [foreign concept].
#
#   The foreign relation stops after the first pause (ignoring initial pause due
#   to a first vowel word)
p_foreign_relation_n_open <- space? (m a)

# - Grammatical quotation
#   Quote a grammatical valid text
#   x1 is text [quoted text]
p_grammatical_quote_open <- space? (m e)
p_grammatical_quote_close <- space? (m e i)

# - One word quote
#   x1 is text [quoted word]
p_one_word_quote <- space? (m i)

# - Unrammatical quotation
#   Quote an ungrammatical valid text
#   x1 is text [quoted text]
p_ungrammatical_quote_open <- space? (m o)
p_ungrammatical_quote_close <- space? (m o i)

# - Free quote
#   Quote any string with provided quote word.
#   x1 is text [quoted text]
p_foreign_quote <- space? (m u)

# - Relation place swaping
p_place_swap <- space? (s (e / i / o / u / e i)) # 2 / 3 / 4 / 5 / question

# - Predicate numeral place tag
p_predicate_place <- space? (f vowel / v i / v e)  # 1..5 places / place question / next numeral place

# - Predicate tail
p_predicate_tail_open_elidible <- p_predicate_tail_open? 
p_predicate_tail_open <- space? (v a)
p_predicate_tail_close_elidible <- (space? (v a i))?

# - Forethought connectives
p_pre_connective_open <- space? (g a)
p_pre_connective_separator <- space? (g e)
p_pre_connective_close_elidible <- p_pre_connective_close?
p_pre_connective_close <- space? (g a i)

# - Afterthought connectives
# todo : add mass/set predicate connectives
p_predicate_connective <- space? (j vowel)  # a/e/o/u + i question
p_relation_connective <- space? (c vowel)  # a/e/o/u + i question

# - Negations
p_neg_pre <- space? (n a) 
p_neg_post <- space? (n a i) 

# - Scoping
p_scope_open <- space? (g o)
p_scope_close_elidible <- (space? (g o i))?

# - Literals
p_letter <- space? (consonant y / vowel_y h y / (i / u) y h y / vi_diphtong h y / y h y )  # consonant / vowel / semi-vowel / h

# MORPHOLOGY
# - Forein text quoting
foreign_quote_open <- word
foreign_quote_word <- (!space .)+ 
foreign_quote_close <- word

# - Legal words
foreign_word <- (consonant / consonant_pair)? vowel_tail (consonant_cluster vowel_tail)* (consonant consonant)?
word <-  relation_word / particle_word
particle_word <- consonant? vowel_tail 
# relation_word <- consonant_pair vowel_tail 
relation_word <- (consonant_pair vowel_tail coda?) / (!coda consonant vowel_tail coda)

# - Legal vowels and vowel tails
vowel_tail <- (diphtong / vowel_y) (h (vi_diphtong / vowel_y ))* 

diphtong <- iuv_diphtong / vi_diphtong
iuv_diphtong <- (i / u) vowel_y
vi_diphtong <- (a / e / o / y) i 
vowel_y <- vowel / y 
vowel <- a / e / i / o / u 
h <- ['h] # <LEAF>
a <- [aA] # <LEAF>
e <- [eE] # <LEAF>
i <- [iI] # <LEAF>
o <- [oO] # <LEAF>
u <- [uU] # <LEAF>
y <- [yY] # <LEAF>

# - Legal consonant and consonant pairs
consonant_cluster <- (consonant consonant*)
consonant_pair <- (&initial consonant consonant !consonant)
initial <- (affricate / sibilant? other? liquid?) !consonant

consonant <- (voiced / unvoiced / coda) 
affricate <- (t c / t s / d j / d z) 
liquid <- (l / r) 
other <- (p / t !l / k / f / x / b / d !l / g / v / m / n !liquid) 
sibilant <- (c / s !x / (j / z) !n !liquid) 
coda <- (l / m / n / r) 
voiced <- (b / d / g / j / v / z) 
unvoiced <- (c / f / k / p / s / t / x) 
l <- [lL] !l # <LEAF>
m <- [mM] !m # <LEAF>
n <- [nN] !n !affricate # <LEAF>
r <- [rR] !r # <LEAF>
b <- [bB] !b !unvoiced # <LEAF>
d <- [dD] !d !unvoiced # <LEAF>
g <- [gG] !g !unvoiced # <LEAF>
v <- [vV] !v !unvoiced # <LEAF>
j <- [jJ] !j !z !unvoiced # <LEAF>
z <- [zZ] !z !j !unvoiced # <LEAF>
s <- [sS] !s !c !voiced # <LEAF>
c <- [cC] !c !s !x !voiced # <LEAF>
x <- [xX] !x !c !k !voiced # <LEAF>
k <- [kK] !k !x !voiced # <LEAF>
f <- [fF] !f !voiced # <LEAF>
p <- [pP] !p !voiced # <LEAF>
t <- [tT] !t !voiced # <LEAF>

# - Space / Pause
initial_dot <- (dot &vowel_y / !dot &consonant)
space <- space_1+ (dot &vowel)? / dot &vowel / EOF
space_1 <- space_char / hesitation space_char
hesitation <- space_char+ dot? y+ dot? space_char+ 

# - Special characters
dot <- '.'
space_char <- [\t\n\r?!\u0020]
EOF <- !.