# eberban PEG grammar - v0.4
# ==========================

# GRAMMAR
# main rule, allow language version/dialect annotation
text <- parser_version? text_1
parser_version <- DI_clause (!parser_version_number borrowing_content (dot !dot !y)? parser_version_number? / parser_version_number)
parser_version_number <- spaces? TA+
# main text rule
text_1 <- (free_indicator / free_vocative / free_parenthetical)* (paragraph+ / sentence*) spaces? EOF?

# paragraphs
paragraph <- DA_clause+ sentence*
# a sentence is either a predicate or a fragment
sentence <- predicate / fragments
# fragments : allow to answer questions without making a complete predicate
fragments <- DE_clause (predicate_place / FA_clause)* DEI_clause_elidible

# predicate afterthough connectives
predicate <- predicate_jak_post / predicate_1
predicate_jak_post <- predicate_1 (jak !DO_clause DE_clause_elidible predicate_1)+
# pre-tail terms
predicate_1 <- predicate_jak_pre / DE_clause_elidible predicate_1_terms DO_clause_elidible predicate_tail DEI_clause_elidible
predicate_1_terms <- predicate_place*
# forethough connected predicates
predicate_jak_pre <- (pre_jak !DO_clause DE_clause_elidible predicate (pre_connective_separator predicate)+ GAI_clause_elidible)

# main predicate tail rule
predicate_tail <- predicate_tail_jak_post / predicate_tail_1
# predicate-tail afterthough connectives
predicate_tail_jak_post <- predicate_tail_1 (jak !DE_clause DO_clause_elidible predicate_tail_1 predicate_tail_jak_post_terms)+
predicate_tail_jak_post_terms <- predicate_tail_terms
# predicate-tail negation
predicate_tail_1 <- KA_clause* predicate_tail_2
# simple predicate-tail / forethough connected tails
predicate_tail_2 <- relation predicate_tail_terms / predicate_tail_jak_pre
# forethough connected tails structure
predicate_tail_jak_pre <- (pre_jak !DE_clause DO_clause_elidible predicate_tail (pre_connective_separator DO_clause_elidible predicate_tail)+ GAI_clause_elidible) predicate_tail_jak_pre_terms
predicate_tail_jak_pre_terms <- predicate_tail_terms

# terms followed by predicate-tail elidible terminator
predicate_tail_terms <- predicate_place* DOI_clause_elidible
# place + term
predicate_place <- predicate_place_tag predicate_term

# predicate term connectives
predicate_term <- predicate_term_jak_post / predicate_term_1
predicate_term_jak_post <- predicate_term_1 (jak !DE_clause !DO_clause predicate_term_1)+
predicate_term_1 <- predicate_term_jaik_post / predicate_term_2
predicate_term_jaik_post <- predicate_term_2 (jaik !DE_clause !DO_clause predicate_term_2)+
# simple relation term / forethough connected terms
predicate_term_2 <- predicate_term_jak_pre / scoped_predicate_term / relation
# forethough connected term structure
predicate_term_jak_pre <- (pre_jak !DA_clause !DO_clause predicate_term (pre_connective_separator !DO_clause predicate_term)+ GAI_clause_elidible)
# scoped predicate term
scoped_predicate_term <- GO_clause predicate_term GOI_clause_elidible

# predicate place tags connectives
predicate_place_tag <- predicate_place_tag_jak_post / predicate_place_tag_1
predicate_place_tag_jak_post <- predicate_place_tag_1 (JA_clause predicate_place_tag_1)+

# basic predicate place tags
predicate_place_tag_1 <- predicate_place_modal / FA_clause
# predicate place modal
predicate_place_modal <- DU_clause relation_2

# relation compounds, links and relative clauses
relation <- relation_1+ relation_link*
relation_link <- VA_clause relation VAI_clause_elidible
# relation afterthough connectives
relation_1 <- relation_cak_post / relation_2
relation_cak_post <- relation_2 (cak !DE_clause !DA_clause relation_2)+
# core relations
relation_2 <- relation_cak_pre / lexeme free_post* / borrowing / grammatical_quote / one_word_quote / ungrammatical_quote / foreign_quote / abstraction / relation_place_swap / scoped_relation / MA_clause / free_prefix* spaces? (root / string) free_post*
# forethough connected relations
relation_cak_pre <- (pre_cak !DE_clause !DA_clause relation (pre_connective_separator relation)+ GAI_clause_elidible)

# flat lexeme prefixes
lexeme <- (lexeme_1 / lexeme_2 / lexeme_3 / lexeme_4  / lexeme_n) 
lexeme_1 <- A_clause lexeme_word
lexeme_2 <- E_clause lexeme_word lexeme_word
lexeme_3 <- I_clause lexeme_word lexeme_word lexeme_word
lexeme_4 <- O_clause lexeme_word lexeme_word lexeme_word lexeme_word
lexeme_n <- U_clause (!(dot? U) lexeme_word)+ (dot? U)
lexeme_word <- initial_dot native_word

# borrowings
# borrowing <- ZA_clause borrowing_content dot? free_post*

borrowing <- ZA_clause borrowing_content (dot !dot !y)? free_post*
borrowing_content <- spaces foreign_word (!spaces native_word)*

# quotes
grammatical_quote <- ZE_clause text_1 ZEI_clause
one_word_quote <- ZI_clause spaces? native_word
ungrammatical_quote <- ZO_clause (!ZOI_clause spaces? native_word) ZOI_clause
foreign_quote <- ZU_clause (spaces?) foreign_quote_open spaces foreign_quote_content foreign_quote_close free_post*
foreign_quote_content <- (foreign_quote_word spaces)*

# abstractions
abstraction <- BA_clause predicate BAI_clause_elidible

# relation place swap
relation_place_swap <- SA_clause relation_2

# scoped relation
scoped_relation <- GO_clause relation GOI_clause_elidible

# string (numbers / literals)
string <- (number_string / letter_string) TAI_clause_elidible
number_string <- TA_clause (TA_clause / BY_clause)*
letter_string <- BY_clause (TA_clause / BY_clause)*

# afterthough connectives
jak <- KA_clause? SA_clause? JA_clause KAI_clause? free_post*
jaik <- JAI_clause
cak <- KA_clause? SA_clause? CA_clause KAI_clause?  free_post*

# forethough connectives
pre_jak <- GA_clause SA_clause? JA_clause KAI_clause? free_post*
pre_cak <- GA_clause SA_clause? CA_clause KAI_clause? free_post*
pre_connective_separator <- GI_clause KAI_clause? free_post*

# free prefix
free_prefix <- PA_clause

# free suffix
free_post <- PAI_clause / free_indicator / free_discursive / free_parenthetical / free_subscript / free_vocative
free_indicator <- XA_clause KAI_clause?
free_discursive <- PI_clause relation_2
free_parenthetical <- PO_clause text_1 POI_clause
free_subscript <- PU_clause string
free_vocative <- (CAI_clause KAI_clause? free_indicator*)+ relation_2?

# PARTICLES CLAUSES
A_clause            <- free_prefix* spaces? A
BA_clause           <- free_prefix* spaces? BA 
BAI_clause          <- free_prefix* spaces? BAI free_post*
BAI_clause_elidible <- BAI_clause?
BY_clause           <- free_prefix* spaces? BY
CA_clause           <- free_prefix* spaces? CA free_post*
CAI_clause          <- free_prefix* spaces? CAI
DA_clause           <- free_prefix* spaces? DA free_post*
DI_clause           <- spaces? DI
DE_clause           <- free_prefix* spaces? DE free_post*
DE_clause_elidible  <- DE_clause?
DEI_clause          <- free_prefix* spaces? DEI free_post*
DEI_clause_elidible <- DEI_clause?
DO_clause           <- free_prefix* spaces? DO free_post*
DO_clause_elidible  <- DO_clause? 
DOI_clause          <- free_prefix* spaces? DOI free_post*
DOI_clause_elidible <- DOI_clause?
DU_clause           <- free_prefix* spaces? DU
E_clause            <- free_prefix* spaces? E
FA_clause           <- free_prefix* spaces? FA free_post*
GA_clause           <- free_prefix* spaces? GA
GAI_clause          <- free_prefix* spaces? GAI free_post*
GAI_clause_elidible <- GAI_clause?
GE_clause           <- free_prefix* spaces? GE
GEI_clause          <- free_prefix* spaces? GEI free_post*
GI_clause           <- free_prefix* spaces? GI
GO_clause           <- free_prefix* spaces? GO
GOI_clause          <- free_prefix* spaces? GOI free_post*
GOI_clause_elidible <- GOI_clause?
I_clause            <- free_prefix* spaces? I
JA_clause           <- free_prefix* spaces? JA free_post*
JAI_clause          <- free_prefix* spaces? JAI free_post*
KA_clause           <- free_prefix* spaces? KA free_post*
KAI_clause          <- free_prefix* spaces? KAI
MA_clause           <- free_prefix* spaces? MA free_post*
O_clause            <- free_prefix* spaces? O
PA_clause           <- spaces? PA free_post*
PAI_clause          <- spaces? PAI
PI_clause           <- free_prefix* spaces? PI
PO_clause           <- free_prefix* spaces? PO
POI_clause          <- free_prefix* spaces? POI
PU_clause           <- free_prefix* spaces? PU
SA_clause           <- free_prefix* spaces? SA
TA_clause           <- free_prefix* spaces? TA
TAI_clause          <- free_prefix* spaces? TAI
TAI_clause_elidible <- TAI_clause?
U_clause            <- free_prefix* spaces? U
VA_clause           <- free_prefix* spaces? VA
VAI_clause          <- free_prefix* spaces? VAI
VAI_clause_elidible <- VAI_clause?
XA_clause           <- free_prefix* spaces? XA
ZA_clause           <- free_prefix* spaces? ZA
ZE_clause           <- free_prefix* spaces? ZE
ZEI_clause          <- free_prefix* spaces? ZEI free_post*
ZI_clause           <- free_prefix* spaces? ZI
ZO_clause           <- free_prefix* spaces? ZO
ZOI_clause          <- free_prefix* spaces? ZOI free_post*
ZU_clause           <- free_prefix* spaces? ZU

# PARTICLE FAMILIES
A     <- &particle (a)
BA    <- &particle !(BAI post_word) (b vowel_tail)
BAI   <- &particle (b a i)
BY    <- &particle (consonant y / vowel_y h y / (i / u) y h y / vi_diphthong h y / y h vowel)
CA    <- &particle (c vowel)
CAI   <- &particle !(CA post_word) (c vowel_tail)
DA    <- &particle (d a)
DE    <- &particle (d e)
DEI   <- &particle (d e i)
DI    <- &particle (d i)
DO    <- &particle (d o)
DOI   <- &particle (d o i)
DU    <- &particle (d u)
E     <- &particle (e)
FA    <- &particle (f vowel_tail)
GA    <- &particle (g a)
GAI   <- &particle (g a i)
GE    <- &particle (g e)
GEI   <- &particle (g e i)
GI    <- &particle (g i)
GO    <- &particle (g o)
GOI   <- &particle (g o i)
I     <- &particle (i)
JA    <- &particle (j vowel)
JAI   <- &particle !(JA post_word) (j vowel_tail)
KA    <- &particle (k a)
KAI   <- &particle (k a i)
MA    <- &particle (m vowel_tail)
O     <- &particle (o)
PA    <- &particle (p a vowel_tail_1?)
PAI   <- &particle (p a i)
PI    <- &particle (p i)
PO    <- &particle (p o)
POI   <- &particle (p o i)
PU    <- &particle (p u)
SA    <- &particle (s vowel_tail)
TA    <- &particle !(TAI post_word) (t vowel_tail)
TAI   <- &particle (t a i)
U     <- &particle (u)
VA    <- &particle !(VAI post_word) (v vowel_tail)
VAI   <- &particle (v a i)
XA    <- &particle (x vowel_tail)
ZA    <- &particle (z a i?)
ZE    <- &particle (z e)
ZEI   <- &particle (z e i)
ZI    <- &particle (z i)
ZO    <- &particle (z o)
ZOI   <- &particle (z o i)
ZU    <- &particle (z u)

# MORPHOLOGY
# - Forein text quoting
foreign_quote_open <- native_word
foreign_quote_word <- (!spaces .)+ 
foreign_quote_close <- native_word

# - Legal words
foreign_word <- !coda (initial_consonant_pair / consonant)? vowel_tail_y (consonant_cluster vowel_tail_y)* (consonant consonant?)?
native_word <-  root / particle
particle <- !coda consonant? vowel_tail_y !coda &post_word
root <- ((initial_consonant_pair vowel_tail_y coda?) / (!coda (initial_consonant_pair / consonant) vowel_tail_y coda)) &post_word

# - Legal vowels and vowel tails
vowel_tail <- (diphthong / vowel) vowel_tail_1* 
vowel_tail_1 <- h (vi_diphthong / vowel )

vowel_tail_y <- (diphthong_y / vowel_y) vowel_tail_y_1* 
vowel_tail_y_1 <- h (vi_diphthong_y / vowel_y )

diphthong_y <- iuv_diphthong_y / vi_diphthong_y
iuv_diphthong_y <- (i / u) vowel_y
vi_diphthong_y <- (a / e / o / y) i 
vowel_y <- vowel / y 

diphthong <- iuv_diphthong / vi_diphthong
iuv_diphthong <- (i / u) vowel
vi_diphthong <- (a / e / o) i 
vowel <- a / e / i / o / u 

h <- ['h] # <LEAF2>
a <- [aA] # <LEAF>
e <- [eE] # <LEAF>
i <- [iI] # <LEAF>
o <- [oO] # <LEAF>
u <- [uU] # <LEAF>
y <- [yY] # <LEAF>

# - Legal consonant and consonant pairs
consonant_cluster <- (!(coda coda coda) consonant consonant*)
initial_consonant_pair <- (&initial consonant consonant !consonant)
initial <- (affricate / sibilant? other? liquid?) !consonant
 
consonant <- (voiced / unvoiced / liquid / m / n) 
affricate <- (t c / t s / d j / d z) 
liquid <- (l / r) 
other <- (p / t !l / k / f / x / b / d !l / g / v / m / n !liquid) 
sibilant <- (c / s !x / (j / z) !n !liquid) 
coda <- (l / n / r)
voiced <- (b / d / g / j / v / z) 
unvoiced <- (c / f / k / p / s / t / x) 

l <- [lL] !l # <LEAF>
m <- [mM] !m # <LEAF>
n <- [nN] !n !affricate # <LEAF>
r <- [rR] !r # <LEAF>
b <- [bB] !b !unvoiced # <LEAF>
d <- [dD] !d !unvoiced # <LEAF>
g <- [gG] !g !unvoiced # <LEAF>
v <- [vV] !v !unvoiced # <LEAF>
j <- [jJ] !j !z !unvoiced # <LEAF>
z <- [zZ] !z !j !unvoiced # <LEAF>
s <- [sS] !s !c !voiced # <LEAF>
c <- [cC] !c !s !x !voiced # <LEAF>
x <- [xX] !x !c !k !voiced # <LEAF>
k <- [kK] !k !x !voiced # <LEAF>
f <- [fF] !f !voiced # <LEAF>
p <- [pP] !p !voiced # <LEAF>
t <- [tT] !t !voiced # <LEAF>

# - Spaces / Pause

post_word <- (dot &vowel_y / &consonant / spaces)
initial_dot <- (dot &vowel_y / !dot &consonant)
spaces <- initial_spaces (dot &vowel_y)? / dot &vowel_y / EOF
initial_spaces <- (hesitation / space_char)+
hesitation <- (space_char+ dot? / dot) !(y h y) y+ !(dot dot) (dot? &space_char / &(dot y) / dot / EOF)
space_char <- [\t\n\r?!\u0020]

# - Special characters
dot <- '.'
EOF <- !.